plugins {
    //For publish to bintray
    id 'com.jfrog.bintray' version '1.3.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

def packageGroup = 'net.virtalab'
def packageName = 'mvc-tools'
def packageVersion = '2.2'

repositories {
    mavenLocal()
    maven {
        url 'http://maven.virtadev.net/jcenter'
    }
}

def v = [
        spring     : '4.2.0.RELEASE',
        commonslang: '2.6',
        servlet    : '3.0.1',
        vson       : '0.3.2',
        log4j      : '1.2.17',
        junit      : '4.12',
        mockito    : '1.10.19'

]

dependencies {
    //Spring
    compile 'org.springframework:spring-core:' + v.spring
    compile 'org.springframework:spring-webmvc:' + v.spring
    //Commons for exceptions logging
    compile 'commons-lang:commons-lang:' + v.commonslang
    //Servlet API
    compile 'javax.servlet:javax.servlet-api:' + v.servlet
    //Json Parser
    compile 'net.virtalab:vson:' + v.vson
    //Logger
    compile 'log4j:log4j:' + v.log4j
    //Junit
    testCompile 'junit:junit:' + v.junit
    //Spring Testing
    testCompile 'org.springframework:spring-test:' + v.spring
    testCompile 'org.mockito:mockito-all:' + v.mockito
}

compileJava {
    def javaLevel = 1.7
    sourceCompatibility = javaLevel
    targetCompatibility = javaLevel
}

jar {
    baseName = packageName
}

//To disable tests - uncomment line below
//build.dependsOn.remove("check");

//publish version to bintray
publishing {
    publications {
        Deploy(MavenPublication) {
            from components.java
            groupId packageGroup
            artifactId packageName
            version packageVersion
        }
    }
}
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Deploy']
    pkg {
        repo = 'maven'
        name = packageName
        userOrg = 'virtalab'
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:virtalab/mvc-tools.git'
        version {
            name = packageVersion
            desc = 'Version decription'
            released = new Date()
            //vcsTag = '0.1'
        }
    }
}